/*
 * Note: The JiJ-related jar tasks (such as groovyJar and fullJar) are in the main build.gradle file
 */

tasks.register('modJar', Jar) {
    from sourceSets.mod.output
    manifest.attributes(manifestAttr - ['FMLModType': 'LANGPROVIDER'])
    archiveBaseName.set('mod')
}

tasks.named('jar', Jar).configure {
    from sourceSets.extension.output
    manifest.attributes(manifestAttr + [
            'Automatic-Module-Name': 'org.groovymc.gml',
            'FMLModType': 'LANGPROVIDER'
    ])
}
java.withSourcesJar()

tasks.named('groovydoc', Groovydoc).configure {
    use = true
    source(sourceSets.transform.allSource)
    source(sourceSets.extension.allSource)
}
tasks.register('groovydocJar', Jar) {
    archiveClassifier = 'javadoc'
    from groovydoc.destinationDir
    dependsOn(groovydoc)
}

final transformDest = new File(buildDir, 'libs/transform')
final transformAttr = manifestAttr + [
        'Specification-Title': 'GML-Transform'
]
tasks.register('transformJar', Jar) {
    from sourceSets.transform.output
    manifest.attributes(transformAttr)
    archiveBaseName.set('transform')
    destinationDirectory.set(transformDest)
}
tasks.register('transformSources', Jar) {
    from sourceSets.transform.allSource
    manifest.attributes(transformAttr)
    archiveBaseName.set('transform')
    archiveClassifier.set('sources')
    destinationDirectory.set(transformDest)
}

tasks.register('testJar', Jar) {
    group 'build'
    from sourceSets.test.output
    archiveClassifier = 'test'
    manifest.attributes([
            'Specification-Title': 'GMLTestMod',
            'Specification-Vendor': 'Matyrobbrt',
            'Specification-Version': '1',
            'Implementation-Title': 'GMLTestMod',
            'Implementation-Vendor': 'Matyrobbrt',
            'Implementation-Version': '1.0'
    ])
}
